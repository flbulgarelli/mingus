grammar org.uqbar.mingus.Mingus with org.eclipse.xtext.common.Terminals

generate mingus "http://www.uqbar-project.org/mingus/Mingus"

Program:
	value=Term;

Term returns Term:
	Term0;

Term0 returns Term:
	Term1 | Letrec | Let;

Term1 returns Term:
	Term2 | Abstraction;

Term2 returns Term:
	Term3 ({Application.function=current} argument=Term3)*;

Term3 returns Term:
	Term4 | Suspention | Forcing;

Term4 returns Term:
	Term5 | Variable | NumberLiteral;

Term5 returns Term:
	'(' Term0 ')';

Abstraction:
	'\\' parameter=ID '.' body=Term0;

Suspention:
	'@' value=Term4;

Forcing:
	'!' value=Term4;

Variable:
	name=ID;

NumberLiteral:
	value=INT;

Binding:
	name=ID '=' value=Term1;

Letrec:
	'letrec' bindings+= Binding (SEPARATOR bindings+=Binding)* 'in' body=Term0;

Let:
	'let' bindings+= Binding (SEPARATOR bindings+=Binding)* 'in' body=Term0;

terminal SEPARATOR:
	','
;
