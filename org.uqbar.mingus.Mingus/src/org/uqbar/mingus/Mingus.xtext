grammar org.uqbar.mingus.Mingus with org.eclipse.xtext.common.Terminals

generate mingus "http://www.uqbar-project.org/mingus/Mingus"

Program:
	value=Term;

Term returns Term:
	Term0;

Term0 returns Term:
	Term1 | Letrec;

Term1 returns Term:
	Term2 | Abstraction;

Term2 returns Term:
	Term3 ({Application.function=current} argument=Term3)*;

Term3 returns Term:
	Term4 | Variable | NumberLiteral;

Term4 returns Term:
	'(' Term0 ')';

Abstraction:
	'\\' parameter=ID '.' body=Term0;

Variable:
	name=ID;

NumberLiteral:
	value=INT;

Letrec:
	'letrec' (name=ID) '=' binding=Term1 'in' body=Term0;
